package com.cesar31.formsweb.parser.main;

import com.cesar31.formsweb.parser.main.FormsLex;
import com.cesar31.formsweb.control.*;
import com.cesar31.formsweb.model.*;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.List;

class FormsParser;

parser code {:

	private Symbol cur_token;
	private UserContainer u;

	public FormsParser(FormsLex lex) {
		super(lex);
		this.u = new UserContainer();
	}

	public void setUser(String user) {
		this.u.setUser(user);
	}

	/* Constructor sin cambiar nombre */
	// public parser(FormsLex lex) {
	// 	super(lex);
	// 	this.parsed = true;
	// 	u = new UserContainer();
	// }

	public void report_fatal_error(String message, Object info) {

        //System.out.println("report_fatal_error");
        //System.out.println("Message: " + message);
        //System.out.println("Object info: " + info);

        /* Armando reportes de errores */
        // List<String> expected = new ArrayList<>();
        // List<Integer> tokens = expected_token_ids();
        // for(Integer i : tokens) {
        //     expected.add(symbl_name_from_id(i));
        // }

		// this.u.setError(this.cur_token, symbl_name_from_id(this.cur_token.sym), expected);
	}

	public void syntax_error(Symbol cur_token) {
        List<String> expected = new ArrayList<>();

        this.cur_token = cur_token;
		//Token t = (Token) cur_token.value;
        //System.out.printf("syntax_error: %s, %s\n", symbl_name_from_id(cur_token.sym), t.toString());

        List<Integer> tokens = expected_token_ids();
        // System.out.print("Se esperaba ->");
        for(Integer i : tokens) {
			//System.out.printf("%s, ", symbl_name_from_id(i));
            expected.add(symbl_name_from_id(i));
        }
        //System.out.println("");

		/* Reporte de Errores */
		this.u.setError(cur_token, symbl_name_from_id(cur_token.sym), expected);
	}

	protected int error_sync_size() {
		return 1;
	}

	public boolean isParsed() {
		return u.getErrors().isEmpty();
	}

	public UserContainer getContainer() {
		return u;
	}

:}

terminal Token INIT_SOL, FIN_SOL, INIT_MANY_SOL, FIN_MANY_SOL;
terminal Token PARAM, INTEGER, COLON, SMALLER, GREATER, LBRACE, RBRACE, LBRACKET, RBRACKET, COMMA, EXCL;
terminal Token PLUS, MINUS, TIMES, DIVIDE;
terminal Token ERROR, SYMB, QUOTE, EMPTY, STR_SPACE, OPTION_SPACE;

terminal Token STR, STR_N, DATE, ADD_USER, CRED, DATE_ADD, EDIT_USER, DEL_USER, LOGIN, OLD_USER, NEW_USER, NEW_PASS, DATE_MOD, USER, PASS;
terminal Token NEW_FORM, PARAM_F, ID, TITLE, NAME, THEME, DEL_FORM, EDIT_FORM, ADD_COMP, PARAM_C, FIELD_N, FORM, CLASS, TEXT_FIELD, TEXT_AREA, CHECKBOX, RADIO;
terminal Token FILE, IMG, COMBO, BTN, INDEX, TEXT, ALIGN, REQUIRED, OPTION, ROWS, COLUMNS, URL, DEL_COMP, EDIT_COMP, ID_, USER_C;
terminal Token CENTER, LEFT, RIGHT, JUSTIFY, YES, NO, OPTION_V, STR_NUMBER, DARK, LIGHT;

non terminal solicitude, ini_sol, fin_sol, sol, request, req_u, body, ini_many, fin_many;

non terminal us, pass, date, add_u, add, old_u, new_u, new_p, e_date;
/* formularios */
non terminal forms, id, title, name, theme, user_c;
/* componentes */
non terminal field_n, form, kind, index, text, opt, url, kind_a, req, rows, columns, comp;

non terminal Token str_param, class_o, align, required, kind_t;


solicitude	::=	ini_sol
				| ini_many sol fin_many
				;

ini_many	::=	SMALLER EXCL INIT_MANY_SOL GREATER
				| error EXCL INIT_MANY_SOL GREATER
				| SMALLER error INIT_MANY_SOL GREATER
				| SMALLER EXCL error GREATER
				| SMALLER EXCL INIT_MANY_SOL error
				;

fin_many	::=	SMALLER EXCL FIN_MANY_SOL GREATER
				| error EXCL FIN_MANY_SOL GREATER
				| SMALLER error FIN_MANY_SOL GREATER
				| SMALLER EXCL error GREATER
				| SMALLER EXCL FIN_MANY_SOL error
				;

sol			::=	ini_sol
				| sol ini_sol
				;

ini_sol		::=	SMALLER:q EXCL INIT_SOL COLON request:r
				{:
					u.setResult(q, (String) r);
				:}
				| error EXCL:q INIT_SOL COLON request:r
				{: u.setResult((Token) q, (String) r); :}
				| SMALLER:q error INIT_SOL COLON request:r
				{: u.setResult(q, (String) r); :}
				| SMALLER:q EXCL error COLON request:r
				{: u.setResult(q, (String) r); :}
				| SMALLER:q EXCL INIT_SOL error request:r
				{: u.setResult(q, (String) r); :}
				;

				/* Usuarios */
request		::=	ADD_USER GREATER req_u /* add user */
				{: RESULT = "CREAR"; :}
				| error GREATER req_u
				{: RESULT = "CREAR"; :}
				| ADD_USER error req_u
				{: RESULT = "CREAR"; :}
				| EDIT_USER GREATER req_u /* edit user */
				{: RESULT = "EDIT"; :}
				| EDIT_USER error req_u
				{: RESULT = "EDIT"; :}
				| DEL_USER GREATER req_u /* del user */
				{: RESULT = "DELETE"; :}
				// | error GREATER req_u
				// {: RESULT = "DELETE" :}
				| DEL_USER error req_u
				{: RESULT = "DELETE"; :}
				| LOGIN GREATER req_u /* login user */
				{: RESULT = "LOGIN"; :}
				| LOGIN error req_u
				{: RESULT = "LOGIN"; :}

				/* Formularios */
				| NEW_FORM GREATER forms
				{: RESULT = "NUEVO_FORMULARIO"; :} /* NUEVO_FORMULARIO */
				| error GREATER forms
				{: RESULT = "NUEVO_FORMULARIO"; :}
				| NEW_FORM error forms
				{: RESULT = "NUEVO_FORMULARIO"; :}
				| DEL_FORM GREATER forms
				{: RESULT = "ELIMINAR_FORMULARIO"; :} /* ELIMINAR_FORMULARIO */
				| DEL_FORM error forms
				{: RESULT = "ELIMINAR_FORMULARIO"; :}
				| EDIT_FORM GREATER forms
				{: RESULT = "MODIFICAR_FORMULARIO"; :} /* MODIFICAR_FORMULARIO */
				| EDIT_FORM error forms
				{: RESULT = "MODIFICAR_FORMULARIO"; :}

				/* Componentes para formulario */
				| ADD_COMP GREATER comp
				{: RESULT = "AGREGAR_COMPONENTE"; :} /* AGREGAR_COMPONENTE */
				| error GREATER comp
				{: RESULT = "AGREGAR_COMPONENTE"; :}
				| ADD_COMP error comp
				{: RESULT = "AGREGAR_COMPONENTE"; :}
				| DEL_COMP GREATER comp
				{: RESULT = "ELIMINAR_COMPONENTE"; :} /* ELIMINAR_COMPONENTE */
				| DEL_COMP error comp
				{: RESULT = "ELIMINAR_COMPONENTE"; :}
				| EDIT_COMP GREATER comp
				{: RESULT = "MODIFICAR_COMPONENTE"; :} /* MODIFICAR_COMPONENTE */
				| EDIT_COMP error comp
				{: RESULT = "MODIFICAR_COMPONENTE"; :}
				;

/* add, del, edit user */
req_u		::=	LBRACE CRED COLON LBRACKET body
				| error CRED COLON LBRACKET body
				| LBRACE error COLON LBRACKET body
				| LBRACE CRED error LBRACKET body
				| LBRACE CRED COLON error body
				;

/* operaciones con formularios */
forms		::=	LBRACE PARAM_F COLON LBRACKET body
				| error PARAM_F COLON LBRACKET body
				// | LBRACE error COLON LBRACKET body
				| LBRACE PARAM_F error LBRACKET body
				| LBRACE PARAM_F COLON error body
				;

/* Operaciones con componentes para formulario */
comp		::=	LBRACE PARAM_C COLON LBRACKET body
				| error PARAM_C COLON LBRACKET body
				// | LBRACE error COLON LBRACKET body
				| LBRACE PARAM_C error LBRACKET body
				| LBRACE PARAM_C COLON error body
				;
/* ----------------------------------------------------------- producciones para crear usuario nuevo */
us			::=	USER:a COLON str_param:p
				{:
					u.setNewParam(a, p, "USUARIO");
					// System.out.println("USUARIO");
				:}
				// | error COLON str_param
				| USER error str_param
				| USER COLON error
				;

pass		::=	PASS:a COLON str_param:p
				{:
					u.setNewParam(a, p, "PASSWORD");
					// System.out.println("PASSWORD");
				:}
				// | error COLON str_param
				| PASS error str_param
				| PASS COLON error
				;

date		::=	DATE_ADD:a COLON DATE:p
				{:
					u.setNewParam(a, p, "FECHA_CREACION");
					// System.out.println("FECHA_CREACION");
				:}
				// | error COLON DATE
				| DATE_ADD error DATE
				| DATE_ADD COLON error
				;

add_u		::=	us
				| pass /* usuarios */
				| date
				| old_u
				| new_u
				| new_p
				| e_date
				| id /* formularios y componentes*/
				| title
				| name
				| theme
				| user_c
				| field_n
				| form
				| kind
				| index
				| text
				| kind_a
				| req
				| opt
				| rows
				| columns
				| url
				| error
				;

add			::=	add_u
				| add_u COMMA add
				// | add_u error add
				;

body		::=	LBRACE add RBRACE RBRACKET RBRACE fin_sol
				| error add RBRACE RBRACKET RBRACE fin_sol
				// | LBRACE error RBRACE RBRACKET RBRACE fin_sol
				| LBRACE add error RBRACKET RBRACE fin_sol
				| LBRACE add RBRACE error RBRACE fin_sol
				| LBRACE add RBRACE RBRACKET error fin_sol
				;
/* ----------------------------------------------------------- producciones para crear usuario nuevo */

/* ----------------------------------------------------------- producciones para formularios */
id			::=	ID:a COLON ID_:p
				{: u.setNewParam(a, p, "ID"); :}
				| ID error ID_
				| ID COLON error
				;

title		::=	TITLE:a COLON str_param:p
				{: u.setNewParam(a, p, "TITULO"); :}
				| TITLE error str_param
				| TITLE COLON error
				;

name		::=	NAME:a COLON str_param:p
				{: u.setNewParam(a, p, "NOMBRE"); :}
				| NAME error str_param
				| NAME COLON error
				;

theme		::=	THEME:a COLON kind_t:p
				{: u.setNewParam(a, p, "TEMA"); :}
				| THEME error kind_t
				| THEME COLON error
				;

user_c		::=	USER_C:a COLON str_param:p
				{: u.setNewParam(a, p, "USUARIO_CREACION"); :}
				| USER_C error str_param
				| USER_C COLON error
				;
/* ----------------------------------------------------------- producciones para formularios */

/* ----------------------------------------------------------- producciones para componentes */
field_n		::=	FIELD_N:a COLON str_param:p
				{: u.setNewParam(a, p, "NOMBRE_CAMPO"); :}
				| FIELD_N error str_param
				| FIELD_N COLON error
				;

form		::=	FORM:a COLON ID_:p
				{: u.setNewParam(a, p, "FORMULARIO"); :}
				| FORM error ID
				| FORM COLON error
				;

kind		::=	CLASS:a COLON class_o:p
				{: u.setNewParam(a, p, "CLASE"); :}
				| CLASS error class_o
				| CLASS COLON error
				;

index		::=	INDEX:a COLON STR_NUMBER:p
				{: u.setNewParam(a, p, "INDICE"); :}
				| INDEX error STR_NUMBER
				| INDEX COLON error
				;

text		::=	TEXT:a COLON str_param:p
				{: u.setNewParam(a, p, "TEXTO_VISIBLE"); :}
				| TEXT error str_param
				| TEXT COLON error
				;

kind_a		::=	ALIGN:a COLON align:p
				{: u.setNewParam(a, p, "ALINEACION"); :}
				| ALIGN error align
				| ALIGN COLON error
				;

req			::=	REQUIRED:a COLON required:p
				{: u.setNewParam(a, p, "REQUERIDO"); :}
				| REQUIRED error YES
				| REQUIRED COLON error
				;

opt			::=	OPTION:a COLON OPTION_V:p
				{: u.setNewParam(a, p, "OPCIONES"); :}
				| OPTION error OPTION_V
				| OPTION COLON error
				;

rows		::=	ROWS:a COLON STR_NUMBER:p
				{: u.setNewParam(a, p, "FILAS"); :}
				| ROWS error STR_NUMBER
				| ROWS COLON error
				;

columns		::=	COLUMNS:a COLON STR_NUMBER:p
				{: u.setNewParam(a, p, "COLUMNAS"); :}
				| COLUMNS error STR_NUMBER
				| COLUMNS COLON error
				;

url			::=	URL:a COLON str_param:p
				{: u.setNewParam(a, p, "URL"); :}
				| URL error str_param
				| URL COLON error
				;
/* ----------------------------------------------------------- producciones para componentes */

/* ----------------------------------------------------------- producciones para modificar usuario */
old_u	::=		OLD_USER:a COLON str_param:p
				{:
					u.setNewParam(a, p, "USUARIO_ANTIGUO");
					// System.out.println("USUARIO_ANTIGUO");
				:}
				// | error COLON str_param
				| OLD_USER error str_param
				| OLD_USER COLON error
				;

new_u	::=		NEW_USER:a COLON str_param:p
				{:
					u.setNewParam(a, p, "USUARIO_NUEVO");
					// System.out.println("USUARIO_NUEVO");
				:}
				// | error COLON str_param
				| NEW_USER error str_param
				| NEW_USER COLON error
				;

new_p	::=		NEW_PASS:a COLON str_param:p
				{:
					u.setNewParam(a, p, "NUEVO_PASSWORD");
					// System.out.println("NUEVO_PASSWORD");
				:}
				// | error COLON str_param
				| NEW_PASS error str_param
				| NEW_PASS COLON error
				;

e_date	::=		DATE_MOD:a COLON DATE:p
				{:
					u.setNewParam(a, p, "FECHA_MODIFICACION");
					// System.out.println("FECHA_MODIFICACION");
				:}
				// | error COLON DATE
				| DATE_MOD error DATE
				| DATE_MOD COLON error
				;
/* ----------------------------------------------------------- producciones para modificar usuario */

/* fin solicitud */
fin_sol		::= SMALLER FIN_SOL EXCL GREATER
				| error FIN_SOL EXCL GREATER
				| SMALLER error EXCL GREATER
				| SMALLER FIN_SOL error GREATER
				| SMALLER FIN_SOL EXCL error
				| error
				;

str_param	::=	STR:p {: RESULT = p; :}
				| DATE:p {: RESULT = p; :}
				| ADD_USER:p {: RESULT = p; :}
				| CRED:p {: RESULT = p; :}
				| USER:p {: RESULT = p; :}
				| PASS:p {: RESULT = p; :}
				| DATE_ADD:p {: RESULT = p; :}
				| EDIT_USER:p {: RESULT = p; :}
				| DEL_USER:p {: RESULT = p; :}
				| LOGIN:p {: RESULT = p; :}
				| OLD_USER:p {: RESULT = p; :}
				| NEW_USER:p {: RESULT = p; :}
				| NEW_PASS:p {: RESULT = p; :}
				| DATE_MOD:p {: RESULT = p; :}
				| NEW_FORM:p {: RESULT = p; :}
				| PARAM_F:p {: RESULT = p; :}
				| ID:p {: RESULT = p; :}
				// | ID_:p {: RESULT = p; :}
				| TITLE:p {: RESULT = p; :}
				| NAME:p {: RESULT = p; :}
				| THEME:p {: RESULT = p; :}
				| USER_C:p {: RESULT = p; :}
				| DEL_FORM:p {: RESULT = p; :}
				| EDIT_FORM:p {: RESULT = p; :}
				| ADD_COMP:p {: RESULT = p; :}
				| PARAM_C:p {: RESULT = p; :}
				| FIELD_N:p {: RESULT = p; :}
				| FORM:p {: RESULT = p; :}
				| CLASS:p {: RESULT = p; :}
				| INDEX:p {: RESULT = p; :}
				| TEXT:p {: RESULT = p; :}
				| ALIGN:p {: RESULT = p; :}
				| REQUIRED:p {: RESULT = p; :}
				| OPTION:p {: RESULT = p; :}
				| ROWS:p {: RESULT = p; :}
				| COLUMNS:p {: RESULT = p; :}
				| URL:p {: RESULT = p; :}
				| DEL_COMP:p {: RESULT = p; :}
				| EDIT_COMP:p {: RESULT = p; :}
				| OPTION_V:p {: RESULT = p; :}
				| STR_NUMBER:p {: RESULT = p; :}
				| class_o:p {:RESULT = p; :}
				| align:p {: RESULT = p; :}
				| required:p {: RESULT = p; :}
				| kind_t:p {: RESULT = p; :}
				;

class_o		::=	TEXT_FIELD:p {: RESULT = p; :}
				| TEXT_AREA:p {: RESULT = p; :}
				| CHECKBOX:p {: RESULT = p; :}
				| RADIO:p {: RESULT = p; :}
				| FILE:p {: RESULT = p; :}
				| IMG:p {: RESULT = p; :}
				| COMBO:p {: RESULT = p; :}
				| BTN:p {: RESULT = p; :}
				;

align		::= CENTER:p {: RESULT = p; :}
				| LEFT:p {: RESULT = p; :}
				| RIGHT:p {: RESULT = p; :}
				| JUSTIFY:p {: RESULT = p; :}
				;

required	::=	YES:p {: RESULT = p; :}
				| NO:p {: RESULT = p; :}
				;

kind_t		::=	DARK:p {: RESULT = p; :}
				| LIGHT:p {: RESULT = p; :}
				;